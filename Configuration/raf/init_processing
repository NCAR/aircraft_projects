#!/bin/csh

echo "This script is intended to create final processing scripts and nimbus setup"
echo "files for batch processing.  Current incarnation does research and ferry"
echo "flights.  Scripts will most likely need tweaking to get exact results, but"
echo "this provides a useful first pass."
echo
echo "Make sure all the ads files you want are downloaded, and unwanted ads files have"
echo "been removed (e.g. superfluous ground ads files).  Also make sure extract2d has"
echo "been run and the all the desired 2d files are in place.  Be sure to merge 2d"
echo "files if multiple files exist for one flight."
echo
echo "Usage:"
echo "cd ICE-T/C130_N130AR/Production"
echo "init_processing ICE-T"
echo
echo "Edit run_proc2d to add/remove 2d processing lines."
echo
echo -n "Proceed (y/N) ? "
set proceed = $<

if ($proceed != "y") then
  exit(1)
endif

if ($#argv == 0) then
  echo "Usage:\n init_processing proj_name\n"

  echo 'Prerequisites: ads files have been copied into RAW_DATA_DIR/proj_name and extract2d has been run (i.e. all .2d files exist in $RAW_DATA_DIR/proj/PMS2D/)'
  exit
endif

set proj=$argv[1]
set dat=${DATA_DIR}/${proj}

# ---------------------------------------------------------
# run_all script
#
cat > run_all << "EOFRA"
#!/bin/csh

/usr/bin/Xvfb -screen 1 1280x1024x16 -pn -ac :9 &
set xvpid=$!
sleep 2

setenv DISPLAY :9.1

if ($uid == 20000) then
  setenv DAT /scr/raf/local_productiondata/
else
  setenv DAT ${DATA_DIR}
endif

"EOFRA"

echo setenv DAT '${DAT}'/$proj >> run_all

cat >> run_all << "EOFRA2"

mkdir ${DAT}

nice ./run_nimbus
nice ./reorder_all
nice ./run_proc2d

kill $xvpid
"EOFRA2"


# ---------------------------------------------------------
# run_nimbus script.
#
cat > run_nimbus << "EOFRN"
#!/bin/csh

foreach setup_file ( setup_?f?? )
  nimbus -b $setup_file
end
"EOFRN"


# ---------------------------------------------------------
# ncReorder script.
#
set files=${proj}'?f??.nc'

cat > reorder_all << "EOFREO"
#!/bin/csh

cd ${DAT}

"EOFREO"

echo "foreach file ( $files )" >> reorder_all

cat >> reorder_all << "EOFREO2"
  echo $file
  ncReorder $file temp1.nc
  mv -f temp1.nc $file
  chmod g+w $file
end
"EOFREO2"


# ---------------------------------------------------------
# nimbus setup_* files.
#
foreach file (${RAW_DATA_DIR}/$proj/*[rf]f*.ads)
  set bfname=`basename $file`
  set flight=`echo $bfname | cut -b 17-20`
  echo "if="$file > setup_$flight
  echo "of="${dat}/${proj}${flight}.nc >> setup_$flight
end


# ---------------------------------------------------------
# Fast 2D processing script.
#
cat > run_proc2d << "EOF2D"
#!/bin/csh

"EOF2D"

foreach file (${RAW_DATA_DIR}/${proj}/PMS2D/*2d)
  set bfname=`basename $file`
  set flight=`echo $bfname | cut -b 17-20`
  echo 'process2d ${RAW_DATA_DIR}/'${proj}/PMS2D/${bfname} '-o ${DAT}/'${proj}${flight}.nc >> run_proc2d
end


chmod +x run_all run_nimbus reorder_all run_proc2d
chmod g+w run_all run_nimbus reorder_all run_proc2d setup_*

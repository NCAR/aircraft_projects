#!/usr/sbin/chat -evf
#
ABORT 'NO CARRIER'
ABORT 'NO DIALTONE'
ABORT 'NO ANSWER'
ABORT 'ERROR'
ABORT 'BUSY'
TIMEOUT 3
SAY "....Resetting modem\n"
''			AT
''			ATZE0
#
# explicitly enable hardware handshaking
'OK\r\n'		AT&C1&D2&K3
#
# Verbose connect messages
'OK\r\n'		ATV1W1X4
#
# Set command escape char to ESC (dec 27). Default='+', dec 43
'OK\r\n'		ATS2=27
#
# query model number
'OK\r\n'		AT+CGMM
#
# query revision number
'OK\r\n'		AT+CGMR
#
# Select service type
# Use V.22 or V.32 for connections to UCAR RAS
# V.110 for iridium direct.
#
# 4,0,1=2400bps,V.22bis
# 6,0,1=4800bps,V.32
# 7,0,1=9600bps,V.32
# 65,0,1=300bps,V.110
# 66,0,1=1200bps,V.110
# 68,0,1=2400bps,V.110
# 70,0,1=4800bps,V.110
# 71,0,1=9600bps,V.110
#
'OK\r\n'	 	AT+CBST=71,0,1
#'OK\r\n'	 	AT+CBST=7,0,1
#
# Set V.43bis data compression
#   0,0=none, 1,0=transmit, 2,0=receive, 3,0=both
'OK\r\n'	 	AT+DS=3,0
#
# request service report (ASYNC,SYNC,REL ASYNC, REL SYNC) on connect
'OK\r\n'	 	AT+CR=1
#
# request compression info on connect
'OK\r\n'	 	AT+DR=1
#
# display current signal quality, 0-5
'OK\r\n'	 	AT+CSQ
SAY "Checking signal quality (0-5 bars)...\n"
#
TIMEOUT 120
#
# 008816000025 is the latest Iridium direct phone number.
# 008816000023 is the phone number for Iridium's older dial in system.
# On linux, the ...23 phone number worked when the ...25 phone number didn't.
# Iridium systems are geared toward Windows systems.
#
'OK\r\n'                ATTD008816000023
#'OK\r\n'                ATTD008816000025
SAY "....Dialing, timeout=120\n"
CONNECT		 	''
SAY "\n....Connected\n"
SAY "\n....Entering ppp\n"

#!/bin/csh -x

set path = ($path /opt/nidas/bin)

# Script to ensure miscellanious prcesses are running.  Run out of cron
# periodically, say every 3-5 minutes.

#  dsm process running on acserver - this dsm generally handles UDP packets 
#    (e.g. parcel track packets)
ps ax | grep -w dsm | grep -qv grep
if ($status) then
  nohup dsm >& /tmp/dsm.log &
endif

# status_listener runs in daemon mode (background) by default.
# Use -d option to run in foreground, with debug messages.
ps ax | grep status_listener | grep -qv grep
if ($status) then
  status_listener
endif

ps ax | grep "acTrack2kml -o" | grep -qv grep
if ($status) then
  nohup /opt/local/bin/acTrack2kml -o -v >& /tmp/acTrack2kml.log &
endif

# from DC3 project
#ps ax | grep "acTrack2kml -p DC8 -o" | grep -qv grep
#if ($status) then
#  nohup /opt/local/bin/acTrack2kml -p DC8 -o >& /tmp/acTrack2kml-DC8.log &
#endif

#ps ax | grep listen_sat.pl | grep -qv grep
#if ($status) then
#  nohup /home/local/Systems/scripts/listen_sat.pl >& /tmp/listen_sat.log &
#endif

if ($AIRCRAFT == "C130_N130AR") then

  # Start daemon to transfer Wyoming Cloud Lidar images to the ground via LDM.
  ps ax | grep uwyo_lidar | grep -qv grep
  if ($status) then
    /home/local/Systems/scripts/uwyo_lidar /mnt/r1/uwyo_lidar 2 /tmp/uwyo_lidar.log &
  endif
endif

if ($AIRCRAFT == "GV_N677F") then

  # Start daemon to insert MTP files into the LDM client.
  # note:  more than one ldminsertd can be run at once so match the process to its logfile name.
# ps ax | grep mtpldmd | grep -qv grep
# if ($status) then
#   /home/local/Systems/scripts/ldminsertd /mnt/r1/mtp 3 /tmp/mtpldmd.log &
# endif
  
  # Start daemon to send via UDP values from new MTP files to a DSM.
  ps ax | grep mtpudpd | grep -qv grep
  if ($status) then
    /home/local/Systems/scripts/mtpudpd /mnt/r1/mtp acserver 30101 /tmp/mtpudpd.log &
  endif
  
  # Start udp2sql to receive database update messages from the ground
  ps ax | grep udp2sql | grep -qv grep
  if ($status) then
    /opt/local/bin/udp2sql >& /tmp/udp2sql.log &
  endif
endif

ps ax | grep listen_lightning.pl | grep -qv grep
if ($status) then
  nohup /home/local/Systems/scripts/lightning/listen_lightning.pl >& /tmp/listen_lightning.log &
endif

ps ax | grep camera_symlink.py | grep -qv grep
if ($status) then
  /home/local/Systems/scripts/camera_symlink.py &
endif


ps ax | grep xmlrpc2shell.py | grep -qv grep
if ($status) then
  /home/local/Systems/scripts/xmlrpc2shell.py >& /tmp/xmlrpc2sh.log
endif

# Open Street Maps rendering daemon.
ps ax | grep renderd | grep -qv grep
if ($status) then
  nice /usr/bin/renderd &
endif

# Nagios variable QC checks
ps ax | grep qc_check | grep -qv grep
if ($status) then
  /home/local/raf/nagios-qc/qc_check.sh 30 >& /tmp/qc_check.log &
endif


if ($AIRCRAFT == "GV_N677F") then

  # Start the Remote Instrument Control aircraft switch
  # It will read its configuration from ~/.config/NCAR/Switch.ini
  ps ax | grep ric_switch | grep -qv grep
  if ($status) then
    /home/local/ric/src/switch/ric_switch >& /tmp/ric_switch.log &
  endif
endif

if ($AIRCRAFT == "C130_N130AR") then

  # Start the Remote Instrument Control aircraft switch
  # It reads its configuration from ~/.config/NCAR/SwitchC130.ini
  ps ax | grep ric_switch | grep -qv grep
  if ($status) then
    /home/local/raf/instruments/instctrl/switch/ric_switch -c ~/.config/NCAR/SwitchC130.ini >& /tmp/ric_switch.log &
  endif
endif

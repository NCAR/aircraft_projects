#! /bin/csh -f
#
#  Archive preliminary netCDF files for 2006-502 Progressive Science
#   Flights on 20 January 2006 (RF16, RF17, RF18)
#
#  * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
#  *  Copyright 2006                                                         *
#  *  University Corporation for Atmospheric Research, All Rights Reserved.  *
#  * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
#
#  Note:  This script runs only in a project's "Production/archive"
#          subdirectory under the "dmg" login.
#
#  File written by Ron Ruth  13 March 2006
#
#  Note:  This script runs only in a project's "Production/archive"
#          subdirectory under the "dmg" login.
#
#  File adapted from file $LOCAL/proj/102/Production/arch_ads.102
#   by Ron Ruth  28 January 2K
#  Revised by RLR 19 April 2002 to use DCS "msrcp" instead of MIGS
#
#  To find executable (non-comment) lines (from within `vi'), type:
#   /^[^#]
#
#  Check login (only "dmg" login allowed to run this script)
set user = `whoami`
if ($user != "dmg") then
  echo "${0}:  Only the 'dmg' login is allowed to run this script.  Quitting."
  exit 1
endif
#
#  Get setup information for this project
set PROJ = `pwd | sed -e "s/\(.*\)\([0-9][0-9][0-9]\)\(.*\)/\2/g"`
set result = $status
if ($result != 0) then
  echo " Cannot find project number in this directory path"
  echo "  Quitting."
  exit 1
endif
set YEAR = `grep FY $JLOCAL/proj/$PROJ/Production/proj.info | sed -e "s/FY=//"`
set TYPE = PRELIMINARY
#
#  To be run on "bora"
set OK = `echo $HOSTNAME | grep -c "bora"`
if (! $OK) then
  echo '  Script set to run only on "bora."  Quitting.'
  exit
endif
#
#  Set MSS Read PassWorD if restricted access
set RPWD = ""
#set RPWD = "-rpwd ReadPassword"
#
#  Choose a source directory
set SDIR = "$DATA_DIR/ProgSci"
#
#  1st wave
#set SFILES = ( \
#Jan20/tt_test1.nc \
#Jan20/tt_test2.nc \
#Jan20/tt_test3.nc \
#)
#set DFILES = ( \
#RF16.nc \
#RF17.nc \
#RF18.nc \
#)
#
#  Get started:
echo "#  $#SFILES Job(s) submitted on `date`"
if ($#SFILES != $#DFILES) then
  echo " Number of source and destination file names don't match.  Quitting"
  exit 1
endif
#
set todo = $#SFILES
set num = 1
while ($num <= $todo)
  set SFILE = $SFILES[$num]
  set DFILE = $DFILES[$num]
  if (-r $SDIR/$SFILE) then
    echo \
    msrcp -pe 32767 -pr 41113009 -wpwd RAFDMG $RPWD \
     $SDIR/$SFILE mss:/RAF/$YEAR/$PROJ/$TYPE/$DFILE
    set result = $status
    if ($result == 0) then
      echo "#  msrcp for $TYPE/$DFILE -- OK -- `date`"
    else
      echo "#  msrcp for $TYPE/$DFILE -- Failed -- `date`"
      echo "#            $TYPE/$DFILE: error code $result"
    endif
  else
    echo "#  msrcp for $TYPE/$DFILE -- Failed -- `date`"
    echo "#^G   Cannot access source file $SDIR/$SFILE"
  endif
  @ num ++
end
#
# Done.
echo "#   Completed on `date`"
exit
#
#
#  MSS path for all bitfiles listed below:  /RAF/2005/502/
#
#  1st wave
#  3 Job(s) submitted on Mon Mar 13 15:34:53 MST 2006
#  msrcp for PRELIMINARY/RF16.nc -- OK -- Mon Mar 13 15:37:20 MST 2006
#  msrcp for PRELIMINARY/RF17.nc -- OK -- Mon Mar 13 15:39:23 MST 2006
#  msrcp for PRELIMINARY/RF18.nc -- OK -- Mon Mar 13 15:41:14 MST 2006
#   Completed on Mon Mar 13 15:41:14 MST 2006
#
